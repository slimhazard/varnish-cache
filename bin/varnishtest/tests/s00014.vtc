varnishtest "req_total_timeout elapse during restarts, retries and ESI"

# restarts
varnish v1 -arg "-p req_total_timeout=1 -p max_restarts=5 -p max_retries=5" -vcl {
	import vtc;
	backend b None;

	sub vcl_recv {
		vtc.sleep(0.3s);
		return (restart);
	}

	sub vcl_synth {
		set resp.http.Restarts = req.restarts;
	}
} -start

client c1 {
	txreq
	rxresp
	expect resp.status == 503
	expect resp.http.Restarts >= 3
	expect resp.http.Restarts < 5
} -run

logexpect l1 -v v1 -d 1 -q "Timestamp:Timeout" {
 	expect 0 *	Begin     {^req \d+ restart$}
 	expect * =	Timestamp Timeout
 	expect 0 =	Error     "req_total_timeout elapsed"
 	expect * =	End
} -run

# retries
server s1 -repeat 4 {
	rxreq
	delay 0.3
	txresp
} -start

varnish v1 -vcl+backend {
	sub vcl_backend_response {
		return (retry);
	}

	sub vcl_backend_error {
		set beresp.http.Retries = bereq.retries;
	}
}

logexpect l1 -v v1 -d 0 -q "FetchError" {
	expect 0 *	Begin        {^bereq \d+ retry$}
	expect * =	BerespHeader {^Retries: [34]$}
	expect * =	End
} -start

client c1 {
	txreq
	rxresp
	expect resp.status == 503
} -run

server s1 -break

logexpect l1 -wait

server s1 -start

varnish v1 -vcl+backend {
	sub vcl_backend_response {
		set beresp.http.Retries = bereq.retries;
		return (error);
	}

	sub vcl_backend_error {
		return (retry);
	}
}

logexpect l1 -v v1 -d 0 -q VCL_Error {
	expect 0 *	Begin      {^bereq \d+ retry$}
	expect * =	Timestamp  Timeout
	expect 0 =	FetchError "req_total_timeout elapsed"
	expect * =	VCL_call   BACKEND_ERROR
	expect 0 =	VCL_return retry
	expect 0 =	VCL_Error  {^No more retries after req_total_timeout elapse$}
	expect * =	End
} -start

client c1 -run

server s1 -break
logexpect l1 -wait

# ESI
server s1 {
	rxreq
	txresp -body {
		<html>
		Before include
		<esi:include src="/body"/>
		After include
		</html>
	}
	rxreq
	delay 1.1
	txresp -body {
		Included file
	}
} -start

varnish v1 -vcl+backend {
	sub vcl_backend_response {
		set beresp.do_esi = true;
	}
}

logexpect l1 -v v1 -d 0 -q FetchError -g request {
	expect * *	Begin       {^req \d+ esi}
	expect * =	ReqURL      /body
	expect * =	Timestamp   Timeout
	expect * =	Error       "req_total_timeout elapsed"
	expect * =	RespStatus  503
	expect * =	End

	expect * *	Begin       bereq
	expect * =	BereqURL    /body
	expect * =	Timestamp   Timeout
	expect * =	FetchError  "req_total_timeout elapsed"
	expect * =	BerespStatus 503
	expect * =	End
} -start

client c1 {
	txreq
	rxresp
} -run

logexpect l1 -wait
